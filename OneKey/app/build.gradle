apply plugin: 'com.android.application'
apply plugin: 'com.fernandocejas.frodo'
apply plugin: 'io.fabric'

android {
    signingConfigs {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        releaseKey {
            keyAlias properties.getProperty('releaseKeyAlias')
            keyPassword properties.getProperty('releaseKeyPassword')
            storeFile file(properties.getProperty('releaseStoreFile'))
            storePassword properties.getProperty('releaseStorePassword')
        }
        debugKey {
            keyAlias properties.getProperty('debugKeyAlias')
            keyPassword properties.getProperty('debugKeyPassword')
            storeFile file(properties.getProperty('debugStoreFile'))
            storePassword properties.getProperty('debugStorePassword')
        }
    }
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    compileOptions.incremental = false
    flavorDimensions "versionCode"
    //TODO: This is a workaround when there are libraries using rxjava1.x and others rxjava2.x(rx-preferences) but this increases the method count.
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
        //TODO understand the next two exclusions
        exclude 'META-INF/io.netty.versions.properties'
        exclude 'META-INF/INDEX.LIST'
    }
    defaultConfig {
        applicationId "com.urbit_iot.onekey"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        multiDexEnabled true
    }
    buildTypes {
        debug {
            minifyEnabled false
            // Uses new built-in shrinker http://tools.android.com/tech-docs/new-build-system/built-in-shrinker
            useProguard false
            applicationIdSuffix = ".debug"
            signingConfig signingConfigs.debugKey
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }

        release {
            minifyEnabled false
            useProguard true
            applicationIdSuffix = ".release"
            signingConfig signingConfigs.releaseKey
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }
    }
    // If you need to add more flavors, consider using flavor dimensions.
    productFlavors {
        mock {
            //applicationIdSuffix = ".mock"
            applicationId "com.urbit_iot.onekey.mock"
        }
        prod {
            applicationId "com.urbit_iot.onekey.prod"
        }
    }
    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true);
        }
    }
    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

/*
 Dependency versions are defined in the top level build.gradle file. This helps keeping track of
 all versions in a single place. This improves readability and helps managing project complexity.
 */
dependencies {

    // App's dependencies, including test
    implementation "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:design:$rootProject.supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:support-v4:$rootProject.supportLibraryVersion"
    implementation "com.android.support.test.espresso:espresso-idling-resource:$rootProject.espressoVersion"
    implementation "com.google.guava:guava:$rootProject.guavaVersion"

    //RXJAVA
    implementation "io.reactivex:rxjava:$rootProject.rxjavaVersion"
    implementation "io.reactivex:rxandroid:$rootProject.rxandroidVersion"

    //RX DNS-SD
    implementation "com.github.andriydruk:dnssd:$rootProject.DNSSDVersion"
    implementation "com.github.andriydruk:rxdnssd:$rootProject.rxDNSSDVersion"

    //RX BLE
    implementation 'com.polidea.rxandroidble:rxandroidble:1.4.3'

    //SQLBRITE
    implementation "com.squareup.sqlbrite:sqlbrite:$rootProject.sqlbriteVersion"

    //GSON
    implementation "com.google.code.gson:gson:$rootProject.gsonVersion"

    //DAGGER2
    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.daggerVersion"
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'
    implementation "com.google.dagger:dagger:$rootProject.daggerVersion"

    //libphonenumber
    implementation "com.googlecode.libphonenumber:libphonenumber:$rootProject.libphonenumber"

    //rx-preferences -> Forces RxJava2.x
    implementation "com.f2prateek.rx.preferences2:rx-preferences:$rootProject.rxPreferences"

    //retrofit
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofit"
    implementation "com.burgstaller:okhttp-digest:$rootProject.okhttpDigest"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofitConverterGson"
    implementation "com.squareup.retrofit2:adapter-rxjava:$rootProject.retrofitAdapterRxJava"

    //reactiveWiFi
    implementation 'com.github.pwittchen:reactivewifi:0.3.0'

    //Apache Commons IO
    implementation 'commons-io:commons-io:2.5'

    //Slide to Act View
    implementation 'com.ncorti:slidetoact:0.3.0'
    implementation 'com.github.MAXDeliveryNG:slideview:1.1.0'

    //ping LAN addresses to determine reachable host
    implementation 'com.github.stealthcopter:AndroidNetworkTools:0.3.08'

    //mqtt paho wrapper uses rxjava2
    implementation 'net.eusashead.mqtt:rxmqtt:1.1.1.RELEASE'
    implementation 'com.github.akarnokd:rxjava2-interop:0.12.6'
    implementation 'com.bugfender.sdk:android:1.+'
    implementation 'com.github.tony19:timber-loggly:1.0.1'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.3@aar') {
        transitive = true
    }
    api 'com.google.guava:guava:25.1-android'

    //implementation 'io.reactivex:rxnetty-http:0.5.2'
    implementation 'pl.charmas.android:android-reactive-location2:2.1@aar'
    implementation 'com.google.android.gms:play-services-location:15.0.1'

    // Dependencies for local unit tests
    testImplementation "junit:junit:$rootProject.ext.junitVersion"
    testImplementation "org.mockito:mockito-all:$rootProject.ext.mockitoVersion"
    testImplementation "org.hamcrest:hamcrest-all:$rootProject.ext.hamcrestVersion"

    // Android Testing Support Library's runner and rules
    androidTestImplementation "com.android.support.test:runner:$rootProject.ext.runnerVersion"
    androidTestImplementation "com.android.support.test:rules:$rootProject.ext.runnerVersion"
    androidTestImplementation "junit:junit:$rootProject.ext.junitVersion"
    androidTestImplementation "org.mockito:mockito-core:$rootProject.ext.mockitoVersion"
    androidTestImplementation "com.google.dexmaker:dexmaker:$rootProject.ext.dexmakerVersion"
    androidTestImplementation "com.google.dexmaker:dexmaker-mockito:$rootProject.ext.dexmakerVersion"

    // Espresso UI Testing
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$rootProject.espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$rootProject.espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$rootProject.espressoVersion"

    // Resolve conflicts between main and test APK:
    androidTestImplementation "com.android.support:support-annotations:$rootProject.supportLibraryVersion"
    androidTestImplementation "com.android.support:support-v4:$rootProject.supportLibraryVersion"
    androidTestImplementation "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    androidTestImplementation "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    androidTestImplementation "com.android.support:design:$rootProject.supportLibraryVersion"
}
